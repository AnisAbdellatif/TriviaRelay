defmodule TriviaRelay.RateLimit do
  @moduledoc """
  Fixed-window request counting, per caller and bucket.

  Logging in, searching packs and taking a seat each make calls to Sporcle on the
  caller's behalf and cost them nothing, so those endpoints are metered
  (`TriviaRelayWeb.Plugs.RateLimit`). Copied from FazouraParty's `Fazoura.RateLimit`.
  This is deliberately small: one ETS table of counters, swept periodically. It is a flood
  stop, not a fairness mechanism, and it counts per node — which is all a single-VPS
  deployment has.

  A fixed window lets a caller spend two windows' worth of requests across a boundary.
  That is accepted: the limits are far above real use, and the point is to bound the
  sustained rate, not to police bursts precisely.
  """

  use GenServer

  @table __MODULE__
  # Counters are only useful for the window they belong to; sweep the stale ones so a
  # long-running server doesn't accumulate a row per caller seen.
  @sweep_every :timer.minutes(5)

  @type bucket :: atom()

  def start_link(opts), do: GenServer.start_link(__MODULE__, opts, name: __MODULE__)

  @doc """
  Counts one request from `key` against `bucket`.

  `amount` is how much this one counts for — one request, or a request's bytes for a
  bucket that meters volume. Returns `:ok` while at or under `limit` per `window_ms`, and
  `{:error, :rate_limited}` once over. Missing table (not started, as in some unit
  tests) means no limiting rather than a crash.
  """
  @spec check(bucket(), String.t(), pos_integer(), pos_integer(), pos_integer()) ::
          :ok | {:error, :rate_limited}
  def check(bucket, key, limit, window_ms, amount \\ 1) do
    # The window's start in ms, so a sweep can compare it to wall-clock time without
    # knowing which window length any particular bucket used.
    window_start = div(now_ms(), window_ms) * window_ms

    counter = {bucket, key, window_start, window_ms}
    count = :ets.update_counter(@table, counter, {2, amount}, {counter, 0})

    if count > limit, do: {:error, :rate_limited}, else: :ok
  rescue
    ArgumentError -> :ok
  end

  @doc """
  Whether `key` is already over `limit` in `bucket`'s current window, without counting
  anything. For meters that count failures: the attempt is let through or not by this,
  and only one that fails is counted with `check/4`.
  """
  @spec exceeded?(bucket(), String.t(), pos_integer(), pos_integer()) :: boolean()
  def exceeded?(bucket, key, limit, window_ms) do
    window_start = div(now_ms(), window_ms) * window_ms

    case :ets.lookup(@table, {bucket, key, window_start, window_ms}) do
      [{_key, count}] -> count >= limit
      [] -> false
    end
  rescue
    ArgumentError -> false
  end

  @doc "Forgets every counter. Tests only."
  @spec reset() :: :ok
  def reset do
    :ets.delete_all_objects(@table)
    :ok
  rescue
    ArgumentError -> :ok
  end

  ## Callbacks

  @impl true
  def init(_opts) do
    :ets.new(@table, [:named_table, :public, :set, write_concurrency: true])
    schedule_sweep()
    {:ok, %{}}
  end

  @impl true
  def handle_info(:sweep, state) do
    _swept = sweep()
    schedule_sweep()
    {:noreply, state}
  end

  # A counter whose window has ended is spent. Each counter carries its own window's
  # length, so a bucket counted per day keeps its count all day rather than losing it
  # at the next sweep.
  @doc "Drops every counter whose window has ended. Run on a timer; public for tests."
  @spec sweep() :: non_neg_integer()
  def sweep do
    now = now_ms()

    :ets.select_delete(@table, [
      {{{:_, :_, :"$1", :"$2"}, :_}, [{:<, {:+, :"$1", :"$2"}, now}], [true]}
    ])
  rescue
    ArgumentError -> 0
  end

  defp schedule_sweep, do: Process.send_after(self(), :sweep, @sweep_every)

  defp now_ms, do: System.system_time(:millisecond)
end
